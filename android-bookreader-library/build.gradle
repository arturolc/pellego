buildscript {
    repositories {
        mavenLocal()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath 'com.github.axet:gradle-android-dx:0.0.4'
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'digital.wup.android-maven-publish' // 'gradle publishToMavenLocal' and 'publish' tasks

evaluationDependsOn(":app")

android {
    compileSdkVersion 25
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23 // 24+ - unable to start intent with 'file://'
        versionCode = project(':app').android.defaultConfig.versionCode
        versionName = project(':app').android.defaultConfig.versionName
        consumerProguardFiles 'proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            java {
                srcDirs += project(':app').android.sourceSets.main.java.srcDirs
            }
            res {
                srcDirs += project(':app').android.sourceSets.main.res.srcDirs
            }
        }
    }
}

// https://docs.gradle.org/current/userguide/publishing_maven.html

task javadoc(type: Javadoc) {
    failOnError = false // options.addStringOption('Xdoclint:none', '-quiet')
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

def prop(String name) {
    project.hasProperty(name) ? project.getProperty(name) : null
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.axet'
            artifactId 'android-bookreader-library'
            version android.defaultConfig.versionName

            from components.android
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Android BookReader Library'
                description = 'Android BookReader library classes.'
                url = 'https://gitlab.com/axet/android-book-reader'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'axet'
                        name = 'Alexey Kuznetsov'
                        email = 'axet@me.com'
                    }
                }
                scm {
                    connection = 'scm:git:' + pom.url.get()
                    developerConnection = 'scm:git:' + pom.url.get()
                    url = pom.url
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username prop('ossrhUsername')
                password prop('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    testImplementation 'junit:junit:4.12'
    api 'com.github.axet:android-library:1.33.13' // implementation project(':android-library')
    api 'com.android.support:design:25.3.1'
    api 'com.android.support:support-vector-drawable:25.3.1'
    api 'com.android.support.constraint:constraint-layout:1.1.3'
    api 'com.github.axet.fbreader:library:0.1.17'
    api 'com.github.axet:djvulibre:0.1.4'
    api 'com.github.axet:pdfium:2.0.14'
    api 'com.github.axet:k2pdfopt:0.1.6'
    api ('com.github.axet:wget:1.6.1') { exclude group: 'org.json', module: 'json' }
}
